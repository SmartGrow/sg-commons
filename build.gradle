allprojects {
  group = 'sg-commons'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    compile("org.apache.commons:commons-lang3:3.6")
    compileOnly("org.projectlombok:lombok:1.16.18")

    testCompile("junit:junit:4.12")
    testCompile("org.mockito:mockito-all:1.9.5")
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  task packageTests(type: Jar) {
    from sourceSets.test.output
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        groupId project.group
        artifactId project.name
        version project.version
        from components.java

        artifact sourceJar {
          classifier "sources"
        }

        artifact packageTests {
          classifier "tests"
        }
      }
    }
  }

  buildscript {
    ext {
      springDependencyManagementVersion = '0.5.2.RELEASE'
      springBootVersion = '1.5.6.RELEASE'

    }
    repositories {
      mavenCentral()
    }
    dependencies {
      classpath("io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}")
      classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
   }
}

configure(subprojects.findAll {it.name != 'sg-springboot-core'}) {
  dependencies {
    compile project(':sg-springboot-core')
  }
}
